module Trader.IOU where

data Currency
 = CNY
 | USD
 | GBP
    deriving (Eq, Show)

data Cash = Cash with
  amount: Decimal
  currency: Currency
    deriving (Eq, Show)

template IOUProposal
  with
    iou: IOU
  where
    signatory iou.issuer
    observer iou.owner

    choice IOUProposal_Accept: ContractId IOU
      controller iou.owner
      do
        create iou

template IOU
  with
    issuer: Party
    owner: Party
    cash: Cash
  where
    signatory issuer, owner
    observer owner
    ensure cash.amount > 0.0

    choice Make_IOUTransferProposal: ContractId IOUTransferProposal
      with newOwner: Party
      controller owner
      do
        assertMsg "newOwner can't be the same as the origin owner" (owner /= newOwner)
        create IOUTransferProposal with
          iou = this
          newOwner

template IOUTransferProposal
  with
    iou: IOU
    newOwner: Party
  where
    signatory (signatory iou)
    observer (observer iou), newOwner
    
    choice IOUTransferProposal_Accept: ContractId IOU
      controller newOwner
      do
        create iou with owner = newOwner

    choice IOUTransferProposal_Cancel: ContractId IOU
      controller iou.owner
      do
        create iou
    
    choice IOUTransferProposal_Reject: ContractId IOU
      controller newOwner
      do
        create iou